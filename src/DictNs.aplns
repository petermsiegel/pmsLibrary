Dict← { 
  ⍝H ---------------------------------------------------------------------------------
  ⍝H --------------------------------------------------------+
  ⍝H    Dict: A small Dictionary utility                     |
  ⍝H          Keys and values may have any shape and type    |
  ⍝H --------------------------------------------------------+
  ⍝H [a] d← [default] Dict keyList valList   where vectors (keyList ≡⍥≢ valList)
  ⍝H                       ↓⍉↑ kv1 kv2 ...   where kvN is a (key value) pair, e.g. ('name' 'Terry Dactyl')
  ⍝H [b] d← [default] Dict ⍬ ⍬               generates a dictionary with empty lists 
  ⍝H                       ⍬                 same as (Dict ⍬ ⍬)
  ⍝H Returns a dictionary namespace <d>, setting
  ⍝H        [a]  its keys to keyList and values to valList, if <keyList valList> is presented;
  ⍝H        [b]  no initial keys or values, if ⍬ ⍬ or ⍬.
  ⍝H   If <default> is defined, that will be used as the default value for Get (etc) for missing keys.
  ⍝H   If not, ⍬ is used as the default value.  (See variable d.Default).
  ⍝H   A useful default for counters is 0.
  ⍝H
  ⍝H ---------------------------------------------------------------------------------
  ⍝H                                         
  ⍝H Basic:                       
  ⍝H    Creating Objects:       d←Dict     d.Copy
  ⍝H    Setting and Getting:    d.Set      d.Get      d.Default[←...]
  ⍝H    Sorting Values:         d.SortBy   d.SortIP 
  ⍝H    Deleting K-V Pairs:     d.Del
  ⍝H    Displaying All          d.Keys     d.Vals      d.Items
  ⍝H Advanced:
  ⍝H   Modifying Values:        d.Do       d.Cat   
  ⍝H
  ⍝H For Help:   Dict 'Help' 
  ⍝H             d.Help, if d is a dictionary.
  ⍝H 
    ⎕IO ⎕ML←0 1 
    d←(calr←⊃⎕RSI).⎕NS''  ⋄ _←d.⎕DF (⍕calr),'.[Dict]'
  
  ⍝ _Err: (Internal) Error Signaller
    d._Err← ⎕SIGNAL {⊂⎕DMX.(('EM' ('Dict: ',EM ⍵⊃⍨0≠≢⍵))('EN' (EN 11⊃⍨EN∨0≠≢⍵))('Message' Message))}

  ⍝H ======================================
  ⍝H =======        BASIC        ==========
  ⍝H ======================================

  ⍝H d.Copy
  ⍝H d2← d.Copy
  ⍝H   Returns a complete, independent copy (clone) of dictionary d.
  ⍝H
    _←d.⎕FX 'd2←Copy'  'd2←Default Dict _keys _vals'

  ⍝H d.Defined
  ⍝H [1|0]← d.Defined key
  ⍝H Returns 1 if <key> is defined, else 0.
  ⍝H
    d.Defined←{ (≢_keys)>_keys⍳⊂⍵ }

  ⍝H d.Del (or d.Delete)
  ⍝H [1|0]← [quiet] d.Del key
  ⍝H   If the key <key> exists, deletes the entry (key value pair).
  ⍝H      Returns: shy 1 (deleted).
  ⍝H   If it does NOT exist:
  ⍝H      if quiet=1: 
  ⍝H          returns shy 0 (not found);
  ⍝H      otherwise (quiet=0): 
  ⍝H          signals an error (⎕EN=3 Index Error).
  ⍝H
    d.Del← d.Delete← { 
          ⍺← 0 ⋄ k← ⍵ 
      (≢_keys)>p←_keys⍳⊂k: _←1⊣ (_keys _vals) /⍨← ⊂ p≠⍳≢_keys
      ⍺: _←0  ⋄ 3 ⎕SIGNAL⍨'Dict.Del: Key Not Found (⍺:quiet option not selected).'
    }

  ⍝H d.Get
  ⍝H val← [nonceDefault] d.Get key
  ⍝H Returns: 
  ⍝H   - the value <val> for <key>, if <key> defined.
  ⍝H   - Otherwise, returns a default as specified here:
  ⍝H     - The default is nonceDefault, if nonceDefault defined, 
  ⍝H     - Otherwise, the default is that defined when the dictionary was 
  ⍝H       created via d←Dict... 
  ⍝H If not explicitly specified by the user, the fundamental default (Default) is ⍬.
  ⍝H
    d.Get←{ 
          ⍺← ⊢ ⋄ k← ⍵ 
      (≢_keys)>p←_keys⍳⊂k: p⊃_vals 
      0≡⍺ 0: Default ⋄ ⍺
    }

  ⍝H d.Help
  ⍝H   Provides this helpful information.
  ⍝H   Alternatively, execute:   Dict 'Help'
  ⍝H
    _←d.⎕FX 'Help' '_Help ⍬'
    d._Help←{0=≢_h←'^\h*⍝H(.*)' ⎕S '\1'⊣⎕NR 'Dict': 'No help available' ⋄ ⎕ED '_h'}

  ⍝H d.Items
  ⍝H i← d.Items
  ⍝H Returns all the keys and their values as key-value pairs.
  ⍝H
    _←d.⎕FX 'i←Items' 'i←↓⍉↑_keys _vals'

  ⍝H d.Set
  ⍝H {val}← d.Set key val    OR:   {val}← key d.Set val
  ⍝H   Sets value for key key to value val. 
  ⍝H   If it exists, it is overwritten.
  ⍝H Shyly returns the value <val> just set.
  ⍝H ∘ Handy: Set keys and values as pairs (k v):
  ⍝H   d.Set¨ (k1 v1)(k2 v2)...
  ⍝H ∘ Handy: Set keys and values as separate lists (k1 k2 k3) and (v1 v2 v3)
  ⍝H   k1 k2 k3 d.Set¨ v1 v2 v3
  ⍝H
    d.Set←{  
          ⍺←⊢ ⋄ k v←⍺ ⍵
      (≢_keys)>p← _keys⍳⊂k: _← (p⊃_vals)←v  
      1: _←v ⊣ (_keys _vals),← (⊂k) (⊂v)  
    }

  ⍝H SortBy
  ⍝H {dict}← [dict|d] d.SortBy sortFields
  ⍝H   Sorts into dictionary dict, created via dict←Dict ...  
  ⍝H     By default, sorts in place, returning the current dictionary (not a copy).
  ⍝H   If sortFields is empty, sorts using d.keys. 
  ⍝H     Otherwise, (≢sortFields) MUST≡ (d.keys).
  ⍝H Returns:
  ⍝H   dict omitted or Dict≡d:     the current dictionary.
  ⍝H   dict presented and dict≢d:  that dictionary.
  ⍝H Examples: 
  ⍝H   d.SortBy ⍬                  - Sorts d* by keys                          *=sorted in place
  ⍝H   d.(SortBy Keys)             - Sorts d* by keys
  ⍝H   d.(SortBy ⎕C Keys)          - Sorts d* by keys, ignoring case 
  ⍝H   d.(SortBy ⌽Vals)            - Sorts d* by values in descending order 
  ⍝H   dict← (Dict ⍬) d.SortBy ⍬   - Sorts d by keys into a new dictionary (d unchanged). dict has ⍬ as default.   
  ⍝H   dict← d.(Copy SortBy Vals)  - Sorts d by Values into dict (d unchanged). dict takes on d's default value.
  ⍝H 
    d.SortBy←{ 
        ⍺←⎕THIS ⋄ flds← ⍵ _keys⊃⍨ 0=≢⍵
        _keys ≢⍥≢ flds: _Err 'SortBy: Sort field array (⍵) has incorrect number of elements.'
        ⍺.(_keys _vals)←_keys _vals    ⍝ this does nothing if ⍺ and ⎕THIS are the same...
        ⍺.(_keys _vals)⌷⍨← ⊂⊂⍋ flds
      1: _←⍺
    }

  ⍝H SortIP
  ⍝H {d}← d.SortIP
  ⍝H   Sorts dictionary <d> in place in ascending order. Returns shy <d>.
  ⍝H
    _←d.⎕FX '{d}←SortIP'  'd←SortBy _keys'

  ⍝H d.Keys
  ⍝H kk← d.Keys (R/O)
  ⍝H Returns the list of keys
  ⍝H
  _← d.⎕FX 'kk← Keys' 'kk←_keys'

  ⍝H d.Vals
  ⍝H vv← d.Vals (R/O)
  ⍝H Returns the list of values
  ⍝H
  _← d.⎕FX 'vv← Vals' 'vv←_vals'

  ⍝H d.Default  (namespace variable)
  ⍝H ...← d.Default
  ⍝H     Gets the default value.
  ⍝H Returns the current default value (when a key is not found)
  ⍝H 
  ⍝H d.Default← <any data value>
  ⍝H     Sets the default value to use when keys are missing.
  ⍝H     Normally, the default is set through Dict (q.v.), but it may
  ⍝H     be set via assignment as above.
  ⍝H  
  ⍝H Note: Do not expunge d.Default. Bad things will happen!
  ⍝H

  ⍝H ======================================
  ⍝H =======    ADVANCED         ==========
  ⍝H ======================================
  ⍝H d.Do
  ⍝H newVal← key (op d.Do) val       ⍝  key=⍺, op=⍺⍺, val=⍵
  ⍝H   Performs:    key Set (Get key) op ⍵   
  ⍝H Shyly returns: the new value
  ⍝H -------------------------------------
  ⍝H ∘ Example: Dictionary <counter>
  ⍝H   Increment a counter (initially 0) named 'jack' to 1
  ⍝H      counter← 0 Dict ⍬                  ⍝ Set defaults to 0
  ⍝H     'jack' +counter.Do 1                ⍝ Sets entry jack to 0+1  => 1
  ⍝H     'jack' +counter.Do 2                ⍝ Sets entry jack to 1+2  => 3
  ⍝H     'jack' *counter.Do 2                ⍝ Sets entry jack to 3*2  => 9...
  ⍝H 
    d.Do← { 0:: _Err ⍬ ⋄ 1: _←⍺ Set (Get ⍺) ⍺⍺ ⍵ }

  ⍝H d.Cat  
  ⍝H newVal← key d.Cat item
  ⍝H    Treats the value for <key> as a list (vector of vectors) and 
  ⍝H    appends <item> itself to the end of that list, conceptually:
  ⍝H          value← value,⊂item
  ⍝H Shyly returns the new value.
  ⍝H ----------------------------------------------
  ⍝H ∘ Example: Create a dictionary of word lists:
  ⍝H     french← Dict ⍬
  ⍝H   Let's create list 'one'  in our dictionary <french> and append to it:
  ⍝H                                  | Before exec'n   |  After exec'n
  ⍝H     'one' french.Cat  'un'   ==> | ⍬               |   un             
  ⍝H     'one' french.Cat  'une'  ==> | ⊂'un            |   un  une       
  ⍝H     'one' french.Cat  '1'    ==> | 'un' 'une'      |   un  une  1    
  ⍝H ∘ Example: Add a list of items to wordlist 'french'            
  ⍝H   french← Dict ⍬                                  french← Dict ⍬                             
  ⍝H ⍝ Appends three items to list, one at a time.   ⍝ This appends one item containing 3 items!
  ⍝H   ('one'french.Cat)¨ '2' 'deux' 'dos'             'one' french.Cat  '2' 'deux' 'dos'     
  ⍝H    french.Get 'one'                               french2.Get 'one'
  ⍝H    ┌─┬────┬───┐                                   ┌────────────┐
  ⍝H    │2│deux│dos│                                   │┌─┬────┬───┐│
  ⍝H    └─┴────┴───┘                                   ││2│deux│dos││
  ⍝H                                                   │└─┴────┴───┘│
  ⍝H                                                   └────────────┘
  ⍝H
    d.Cat← { 0:: _Err ⍬ ⋄ 1:  _← ⍺ Set (Get ⍺),⊂⍵ }

  ⍝ Executive 
      ⍺← ⍬
      d.Dict← ∇
  'help'≡⎕C ⍵: d.Help
  (2≠≢⍵)∧⍬≢⍵:  d._Err 'DOMAIN ERROR. (See Dict.Help)'
      k v← ,¨⊆ ⍵ 
      d.(Default (_keys _vals) )← ⍺  (k v)  
      d
 }