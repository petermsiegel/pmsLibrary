 {paths}←PlotCollatz numbers
 ;p;Commas;CollatzPaths;Causeway;⎕PP

  ⎕PP←34
  ⍝ See Collatz.aplf
  ⍝ Try PlotCollatz on each of these...
  ⍝   9663 63728127 ¯637281271
  ⍝ PlotCollatz plots the collatz numbers specified...
  ⍝
  ⍝ Info on Collatz Numbers:
  ⍝      Info:   https://en.wikipedia.org/wiki/Collatz_conjecture
  ⍝      Video:  https://www.youtube.com/watch?v=094y1Z2wpJg
  ⍝      Info:   https://tinyurl.com/5crz8rpw
  ⍝

 CollatzPaths←{
     ⎕IO←0
     ⍬::⎕SIGNAL/⎕DMX.{
         11 9≡EN ENX:11,⍨⊂'RANGE ERROR: A Collatz num (c) not in int. range. Cannot evaluate 2|c.'
         EN,⍨⊂EM,((×≢∘⊢)(/∘⊢)': ',⊢)Message
     }0
     0∊⍵=⌊⍵:11 ⎕SIGNAL⍨'Collatz numbers must be integers'

     ⍝ We stop at the END of the (first of the infinitely) repeating cycles
     ⍝   For c>0, the cycle begins at 4 and ends at 1
     ⍝   For c=0, it ends at 0
     ⍝   For c<0, it begins/ends at ¯1/¯2, ¯5/¯10, ¯17/¯34

     EndNow←∊∘1 0 ¯2 ¯10 ¯34
     Collatz1←{                ⍝ Pedagogical Style-- performance improvements abound
         ⍺←⍬
         EndNow ⍵:⍺            ⍝ Done? ==> Return current state vector
         2|⍵:(⍺,⍵)∇ 1+3×⍵      ⍝   Odd? => Next c ← 1 + 3 × c
         (⍺,⍵)∇ ⍵×0.5          ⍝   Even?=> Next c ← c ÷ 2
     }

  ⍝ Calculate
     Collatz1¨⍵
 }

 paths←CollatzPaths numbers

:Section Plot
    Causeway←⎕NS''
    Causeway.⎕CY'SharpPlot'
    Causeway.InitCauseway ⍬
    sp←⎕NEW Causeway.SharpPlot

    sp.SetBackground Causeway.Color.FloralWhite Causeway.FillStyle.Solid
    sp.FrameStyle←Causeway.FrameStyles.Boxed
    sp.LineGraphStyle←Causeway.LineGraphStyles.(Markers+HaloMarkers+XYPlot)

    sp.Heading←'Collatz Path','s'/⍨1≠≢numbers

    Commas←{2↓∊(⊂', ')∘,∘⍕¨⍵}
    sp.Subheading←'for C= ',(Commas numbers),'   Len=',(Commas ≢¨paths),'   Magn= ',(Commas ⌈/∘|¨paths)

    :For p :In paths
        sp.DrawLineGraph p(⍳≢p)
    :EndFor
    {}3500⌶sp.RenderSvg 2
:EndSection

↑numbers{⍺,'  Path length: ',≢⍵}¨paths