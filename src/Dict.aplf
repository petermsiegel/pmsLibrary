Dict← { 
  ⍝H ---------------------------------------------------------------------------------
  ⍝H --------------------------------------------------------+
  ⍝H    Dict: A small Dictionary utility                     |
  ⍝H          Keys and values may have any shape and type    |
  ⍝H --------------------------------------------------------+
  ⍝H [a] d← [default] Dict keyList valList   where vectors (keyList ≡⍥≢ valList)
  ⍝H                       ↓⍉↑ kv1 kv2 ...   where kvN is a (key value) pair, e.g. ('name' 'Terry Dactyl')
  ⍝H [b] d← [default] Dict ⍬ ⍬               generates a dictionary with empty lists of keys and values
  ⍝H                       ⍬                 shortcut for: Dict ⍬ ⍬
  ⍝H   Returns a dictionary namespace <d>, setting
  ⍝H        [a]  its keys to keyList and values to valList, if <keyList valList> is presented;
  ⍝H        [b]  no initial keys or values, if ⍬ ⍬ or ⍬.
  ⍝H   If <default> is defined, that will be used as the default value for Get (etc) for missing keys.
  ⍝H   If not, ⍬ is used as the default value.  (See variable d.defaultG).
  ⍝H   A useful default for counters is 0.
  ⍝H
  ⍝H ---------------------------------------------------------------------------------
  ⍝H                                         
  ⍝H Basic:                       
  ⍝H    Creating Dictionaries:   d←Dict     d.Copy
  ⍝H    Setting and Getting: 
  ⍝H       [Single Item]         d.Set      d.Get      
  ⍝H       [Items]               d.SetX     d.GetX 
  ⍝H       [Default Values]      d.SetDef   d.GetDef 
  ⍝H    Sorting Items:           d.SortBy    
  ⍝H    Deleting Items:          
  ⍝H       [Single Item by Key]  d.Del
  ⍝H       [Items by Key]        d.DelX
  ⍝H       [Items by Index]      d.DelI                   ⎕IO=0
  ⍝H    Displaying All           d.Keys     d.Vals      
  ⍝H       [Items]               d.Items
  ⍝H Advanced:
  ⍝H   Modifying Values:         d.Do(X)    d.Cat(X)   
  ⍝H
  ⍝H For Help:   Dict 'Help' 
  ⍝H             d.Help, if d is a dictionary.
  ⍝H 
    ⎕IO ⎕ML←0 1 
    d←(calr←⊃⎕RSI).⎕NS''  ⋄ _←d.⎕DF (⍕calr),'.[Dict]'
  
  ⍝ _Err: (Internal) Error Signaller
    d._Err← ⎕SIGNAL {
      ⍺← ⎕DMX.(11 EN⊃⍨(×EN)∧0=≢⍵)  ⋄ ⊂⎕DMX.(('EM' ('Dict: ',EM ⍵⊃⍨0≠≢⍵))('EN' ⍺)('Message' Message))
    }

  ⍝H ======================================
  ⍝H =======        BASIC        ==========
  ⍝H ======================================

  ⍝H d.Copy
  ⍝H d2← d.Copy
  ⍝H   Returns a complete, independent copy (clone) of dictionary d.
  ⍝H
    _←d.⎕FX 'd2←Copy'  'd2←defaultG Dict keysG valsG'

  ⍝H d.Defined
  ⍝H [1|0]← d.Defined key
  ⍝H key: an object of any shape
  ⍝H   Returns 1 if <key> is defined, else 0.
  ⍝H
    d.Defined← { (≢keysG)> keysG⍳ ⊂⍵ }
  ⍝H d.DefinedX
  ⍝H [1|0]← d.DefinedX keys
  ⍝H   Returns 1 for each key k in <keys> which is defined;
  ⍝H   Returns 0 for each otherwise.
  ⍝H
    d.DefinedX←{ (≢keysG)> keysG⍳  ⍵ }

  ⍝H d.Del 
  ⍝H  [1|0]← [quiet←0] d.Del key
  ⍝H  key:   an object of any shape
  ⍝H  quiet: scalar 1 or 0
  ⍝H 
  ⍝H  If the key <key> exists, deletes the entry (key value pair).
  ⍝H  If not, does nothing.
  ⍝H    Returns shyly 
  ⍝H      If the key exists: 
  ⍝H         1 (deleted).
  ⍝H      If it does NOT exist:
  ⍝H        if quiet=1: 
  ⍝H            0 (not found);
  ⍝H         otherwise (quiet=0): 
  ⍝H            signals an error (⎕EN=3 Index Error).
  ⍝H
    d.Del← { 
          ⍺← 0 ⋄ k← ⍵ ⋄ p←keysG⍳⊂k    
      (≢keysG)> p: _←1⊣ (keysG valsG) /⍨← ⊂ 0@ p⊢ 1⍴⍨ ≢keysG
      ⍺: _←0  ⋄ 3 ⎕SIGNAL⍨'Dict.Del: Key Not Found (⍺:quiet option not selected).'
    }

  ⍝H d.DelX  
  ⍝H [1|0]← [quiet←0] d.DelX keys
  ⍝H   keys:  vector of keys, each of any shape
  ⍝H   quiet: scalar 1 or 0
  ⍝H
  ⍝H Functional Equivalent to: 
  ⍝H   [[1|0]...]← [quiet←0] d.Del¨ keys
  ⍝H If quiet=0 and at least one key is not found, does nothing.
  ⍝H Otherwise, deletes each key found (returning a 1) and ignores the others (returning a 0).
  ⍝H See d.Del for details.
  ⍝H  
    d.DelX←  { 
          ⍺← 0 ⋄ kk← ⍵ ⋄ f← (≢keysG)> p← keysG⍳ kk
        (0∊f)∧~⍺: 3 _Err 'Key(s) not found'
          (keysG valsG) /⍨← ⊂0@ (f/ p)⊣ 1⍴⍨ ≢keysG
        1: _←f 
    }

  ⍝H d.DelI  
  ⍝H [1|0]←  [quiet←0] d.DelI indices  (⎕IO=0)
  ⍝H    indices: indices for items, where index 0 is the first.
  ⍝H    quiet:   scalar 1 or 0.
  ⍝H
  ⍝H Deletes items (key-value pairs) by index. 
  ⍝H    Unless quiet=1, all indices must be within range of 0..(¯1+≢d.Keys)
  ⍝H    Returns: shy 1 for each key in range, 0 otherwise.
  ⍝H 
    d.DelI←  {  
        0:: _Err ⍬
          ⍺←0 ⋄ f← 0= ⍵⍸⍨ 0 (≢keysG)
        (0∊f)∧~⍺:  3 _Err 'Index Error'
          (keysG valsG) /⍨← ⊂0@ (f/⍵)⊣ 1⍴⍨ ≢keysG
        1: _←f
    }

  ⍝H d.Get
  ⍝H val← [def← GetDef] d.Get key
  ⍝H Retrieves the specified key or <def> if not found.
  ⍝H   Returns: 
  ⍝H     - the value <val> for <key>, if <key> defined.
  ⍝H     - Otherwise, the default <def> (which itself defaults to def← GetDef).
  ⍝H
    d.Get←{ 
        ⍺← defaultG ⋄ k← ⍵ 
      (≢keysG)>p← keysG⍳ ⊂k: p⊃ valsG ⋄ ⍺
    }

  ⍝H d.GetX
  ⍝H val← [defs← GetDef] d.GetX keys
  ⍝H     Logically equivalent to: [defs] d.Get¨ keys
  ⍝H     defs: vector of defaults;  defs must be conformable to keys.
  ⍝H     keys: vector of keys
  ⍝H Retrieves the specified keys or <def>, for those not found.
  ⍝H   Returns: 
  ⍝H     - a vector containing the value <val> for each key in <keys>, if <key> defined.
  ⍝H     - Otherwise, returns a default (if not specified, defs← GetDef)
  ⍝H       - defs must be conformable with keys.
  ⍝H If not explicitly specified by the user, the "default" default (defaultG) is ⍬.
  ⍝H
    d.GetX←{
        kk← ⍵ ⋄ pp← keysG⍳kk  ⋄ ff← pp< ≢keysG
      ~0∊ ff: valsG[ pp ]                            ⍝ All keys found: fast return
        ⍺← ⊂defaultG                                  ⍝ Enclose default (⍺) if implicit only.
      (1≠ ≢⍺) ∧ kk ≠⍥≢ ⍺: 5 _Err 'Length Error: Mismatched left and right argument lengths'
        rr← ⍺⍴⍨ ≢kk                                  ⍝ Prepopulate result with default
      ~1∊ ff: rr                                     ⍝ No keys found: fast return
       valsG[ ff/ pp ]@ (ff/ ⍳≢rr)⊣ rr               ⍝ Enter values for existing keys
    }

  ⍝H d.GetDef   
  ⍝H curDef← d.GetDef
  ⍝H   Gets the default value.
  ⍝H   Returns the current default value (for use when a key is not found)
  ⍝H 
    _← d.⎕FX 'curDef← GetDef' 'curDef← defaultG'

  ⍝H d.Help
  ⍝H   Provides this helpful information.
  ⍝H   Alternatively, execute:   Dict 'Help'
  ⍝H   Returns: nothing
    _←d.⎕FX 'Help' '_Help ⍬'
    d._Help←{0=≢_h←'^\h*⍝H(.*)' ⎕S '\1'⊣⎕NR 'Dict': 'No help available' ⋄ ⎕ED '_h'}

  ⍝H d.Items
  ⍝H i← d.Items
  ⍝H   Returns all the keys and their values as key-value pairs.
  ⍝H
    _←d.⎕FX 'i←Items' 'i←↓⍉↑keysG valsG'

  ⍝H d.Set
  ⍝H {val}← d.Set key val    OR:   {val}← key d.Set val
  ⍝H   Sets value for key key to value val. 
  ⍝H   If it exists, it is overwritten.
  ⍝H Shyly returns the value <val> just set.
  ⍝H ------------
  ⍝H ∘ Handy: Set entries specified as items (kN vN):
  ⍝H   d.Set¨ (k1 v1)(k2 v2)...
  ⍝H ∘ Handy: Set entries specified as separate lists (k1 k2 k3) and (v1 v2 v3)
  ⍝H   k1 k2 k3 d.Set¨ v1 v2 v3
  ⍝H
    d.Set←{  
          ⍺←⊢ ⋄ k v←⍺ ⍵
      (≢keysG)>p← keysG⍳⊂k: _← (p⊃valsG)←v  
      1: _←v ⊣ (keysG valsG),← (⊂k) (⊂v)  
    }

  ⍝H d.SetX
  ⍝H {vals}← d.SetX keys vals    OR:   {vals}← keys d.Set vals
  ⍝H   Sets values for keys <keys> to <vals>.
  ⍝H Shyly returns the values <vals> just set. 
  ⍝H -----------
  ⍝H ∘ Handy: To set entries specified as items (key value pairs):
  ⍝H    d.SetX ↓⍉↑(k1 v1)(k2 v2)....
  ⍝H 
    d.SetX←{  
        ⍺←⊢ ⋄ kk vv←⍺ ⍵
        pp← keysG⍳ kk ⋄ ff← pp< ≢keysG
      ~0∊ff: _← valsG[ pp ]← vv
      ~1∊ff: _← vv⊣ (keysG valsG),← kk vv
        valsG[ ff/ pp ]← ff/ vv
      1: _← vv⊣ keysG valsG,← (⊂~ff)/¨ kk vv 
    }

  ⍝H d. SetDef
  ⍝H oldDef← d.SetDef newDef
  ⍝H     Sets the default value to use when keys are absent from the dictionary.
  ⍝H Returns shyly the old default.
  ⍝H Note: the default is typically set when the dictionary is created:
  ⍝H     MyDefault← ...
  ⍝H     d← MyDefault Dict ⍬ 
  ⍝H  
    d.SetDef←{ 1: _← (defaultG⊢← ⍵)⊢ defaultG }

  ⍝H d.SortBy
  ⍝H {theDict} ← [theDict←d] d.SortBy sortFields sortField
  ⍝H   sortField: a list of vectors, with the same length as d.Keys 
  ⍝H        If ⍬,  d.Keys is used.
  ⍝H   theDict:   a reference to a dictionary (created via Dict or d.Copy).
  ⍝H              If omitted or a reference to <d> itself, sorts in place, rather than making a copy.
  ⍝H   If sortField is empty, sorts using d.keys. 
  ⍝H   Otherwise, if (≢sortField)≢(d.keys), an error is signaled.
  ⍝H Returns theDict (by default: d).
  ⍝H Examples: 
  ⍝H ∘ SORT IN PLACE
  ⍝H   d.SortBy ⍬                  - Sorts d by keys  
  ⍝H   d.(SortBy Keys)             - Sorts d by keys  
  ⍝H   d.(SortBy ⎕C Keys)          - Sorts d by keys, ignoring case  
  ⍝H   d.(SortBy ⌽Vals)            - Sorts d by values in descending order  
  ⍝H ∘ SORT INTO NEW DICTIONARY (ORIGINAL UNCHANGED)
  ⍝H   newD← (Dict ⍬) d.SortBy ⍬   - Sorts d by keys. newD has ⍬ as default.   
  ⍝H   newD← d.(Copy SortBy Vals)  - Sorts d by values. newD takes on d's default value.
  ⍝H 
    d.SortBy←{ 
        ⍺←⎕THIS ⋄ flds← ⍵ keysG⊃⍨ 0=≢⍵
        keysG ≢⍥≢ flds: _Err 'SortBy: Sort field array (⍵) has incorrect length.'
        ⍺.(keysG valsG)←keysG valsG    ⍝ this does nothing if ⍺ and ⎕THIS are the same...
        ⍺.(keysG valsG)⌷⍨← ⊂⊂⍋ flds
      1: _←⍺
    }

  ⍝H d.Keys
  ⍝H kk← d.Keys (R/O)
  ⍝H Returns the list of keys
  ⍝H
  _← d.⎕FX 'kk← Keys' 'kk←keysG'

  ⍝H d.Vals
  ⍝H vv← d.Vals (R/O)
  ⍝H Returns the list of values
  ⍝H
  _← d.⎕FX 'vv← Vals' 'vv←valsG'

  ⍝H ======================================
  ⍝H =======    ADVANCED         ==========
  ⍝H ======================================
  ⍝H d.Do
  ⍝H newVal← key (op d.Do) val       ⍝  key=⍺, op=⍺⍺, val=⍵
  ⍝H   Performs:    key Set (Get key) op ⍵   
  ⍝H   Shyly returns: the new value
  ⍝H -------------------------------------
  ⍝H ∘ Example: Dictionary <counter>
  ⍝H   Increment a counter (initially 0) named 'jack' to 1
  ⍝H      counter← 0 Dict ⍬                  ⍝ Set defaults to 0
  ⍝H     'jack' +counter.Do 1                ⍝ Sets entry jack to 0+1  => 1
  ⍝H     'jack' +counter.Do 2                ⍝ Sets entry jack to 1+2  => 3
  ⍝H     'jack' *counter.Do 2                ⍝ Sets entry jack to 3*2  => 9...
  ⍝H 
    d.Do←  { 0:: _Err ⍬ ⋄ 1: _←⍺ Set  (Get  ⍺) ⍺⍺  ⍵ }
    
  ⍝H d.DoX
  ⍝H newVals← keys (op d.DoX) vals       ⍝  key=⍺, op=⍺⍺, val=⍵
  ⍝H   Performs:    newVals← keys SetX (GetX keys) op¨ ⍵   
  ⍝H   Shyly returns: newVals
  ⍝H See d.Do for details, examples
  ⍝H
    d.DoX← { 0:: _Err ⍬ ⋄ 1: _←⍺ SetX (GetX ⍺) ⍺⍺¨ ⍵ }

  ⍝H d.Cat  
  ⍝H newVal← key d.Cat item
  ⍝H   Treats the value for <key> as a list (vector of vectors) and 
  ⍝H   appends <item> itself to the end of that list, conceptually:
  ⍝H       value← value,⊂item
  ⍝H   Shyly returns the new value.
  ⍝H ----------------------------------------------
  ⍝H ∘ Example: Create a dictionary of word lists:
  ⍝H     french← Dict ⍬
  ⍝H   Let's create list 'one'  in our dictionary <french> and append to it:
  ⍝H                                  | Before exec'n   |  After exec'n
  ⍝H     'one' french.Cat  'un'   ==> | ⍬               |   un             
  ⍝H     'one' french.Cat  'une'  ==> | ⊂'un            |   un  une       
  ⍝H     'one' french.Cat  '1'    ==> | 'un' 'une'      |   un  une  1    
  ⍝H ∘ Example: Add a list of items to wordlist 'french'            
  ⍝H   french← Dict ⍬                                  french← Dict ⍬                             
  ⍝H ⍝ Appends three items to list, one at a time.   ⍝ This appends one item containing 3 items!
  ⍝H   ('one'french.Cat)¨ '2' 'deux' 'dos'             'one' french.Cat  '2' 'deux' 'dos'     
  ⍝H    french.Get 'one'                               french2.Get 'one'
  ⍝H    ┌─┬────┬───┐                                   ┌────────────┐
  ⍝H    │2│deux│dos│                                   │┌─┬────┬───┐│
  ⍝H    └─┴────┴───┘                                   ││2│deux│dos││
  ⍝H                                                   │└─┴────┴───┘│
  ⍝H                                                   └────────────┘
  ⍝H
    d.Cat←  { 0:: _Err ⍬ ⋄ 1: _← ⍺ Set (Get ⍺),⊂⍵     }  

  ⍝H d.CatX
  ⍝H newVals← keys d.CatX items
  ⍝H   Equiv. to:  newVals← keys d.Cat¨ items
  ⍝H See d.Cat for details.
    d.CatX← { 0:: _Err ⍬ ⋄ 1: _← ⍺ SetX (GetX ⍺),∘⊂¨⍵ }  ⍝ Testing...

  ⍝ Executive 
      ⍺← ⍬
      d.Dict← ∇
  'help'≡⎕C ⍵: d.Help
  (2≠≢⍵)∧⍬≢⍵:  d._Err 'DOMAIN ERROR. (See Dict.Help)'
      k v← ,¨⊆ ⍵ 
      d.(defaultG keysG valsG )← ⍺ k v
      d
 }