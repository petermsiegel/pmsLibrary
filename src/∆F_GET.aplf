retc←∆F_GET ;srcFi
⍝ ([retc|⎕EN] retmsg)←  ∆F_GET  
⍝ Import ∆F from GITHUB with requisite ¨help type¨.  
⍝ Prompts for (1) help type: (B)asic-- help inside ∆F itself; (R)ide-- help displayed via URL.
⍝             (2) target namespace: where to locate ∆F.

⍝ Source file: must be defined here!  
  srcFi←  'https://github.com/petermsiegel/pmsLibrary/blob/master/src/∆F.aplf' 
  retc←{
      ⎕IO ⎕ML←0 1 ⋄ NlSp5←(⎕UCS 13),5⍴' ' 

  ⍝ Prompts
      ⎕← 'Prompting for (1) help type (Basic or Ride-based) and (2) target namespace (for ∆F)'
      isR helpTp←  {'R'∊⍵: 1 'RIDE' ⋄ 0 'BASIC'} 1⎕C⍞↓⍨≢ ⍞← 'Help type: BASIC(B) or RIDE(R)? [B] '
      targNs←  '#' {0=≢⍵~' ':⍺ ⋄ ⍵}                 ⍞↓⍨≢ ⍞← 'Target namespace? [#] '
      ⎕← 'Note: For help using ∆F, enter ',targNs,'.∆F⍨''help'''
 
      srcUrl←       ⍵    

  0:: ⎕EN,⊂'Error retrieving ',srcUrl,NlSp5,'Target ns: ',targNs 
      fnNm← ⎕SE.UCMD'get -target=',targNs,' ', srcUrl             ⍝ ]nm← get ...   

  isR:   0,⊂'Created ',fnNm,' with help type ',helpTp             

  0:: ⎕EN,⊂'Created ',fnNm,NlSp5,'Error setting help type: ',helpTp
      SetHelp ← '\(HELP_RIDE *← *1\)' ⎕R '(HELP_RIDE←0)' ⍠ ('ML' ¯1)('Mode' 'D') 
      fixNm← (⍎targNs).⎕FX SetHelp ⎕NR fnNm

  0≠1↑0⍴fixNm: 0, ⊂'Created ',fnNm,' with help type ',helpTp
      ∘😀ERR😀∘
    
  } srcFi
