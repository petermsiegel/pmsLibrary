:Namespace Markdown
:Section Main_Routines 
⍝
⍝ -------------------------------------------------------------------------------------------
⍝ Main routines and declarations
⍝ *** Show *** 
  ⍝ Show:     hNs@ns← newOpts ∇ markdown@CVV
  ⍝ markdown: APL char vectors (CVV)  
  ⍝ newOpts:  New options for size and JSON option variables. Of the form
  ⍝          ('emoji' 0), ('tables' 1), ('size' (500 400)), 1 for Json true and 0 for false.
  ⍝ hNs:      Dyalog Render object (⎕WC namespace)
  ⍝           hNs.HTML contains the generated HTML as a character vector with CR's (via HTMLRenderer)
  ⍝           hNs.MD contains the source markdown used to generate it.
  ⍝ Once the result returned disappears, the generated HTML object disappears also.
  ⍝ Do:              h← size Markdown.Show ... 
  ⍝ Then to delete:  ⎕EX 'h' OR h←''
  Show←{
    0:: ⎕SIGNAL ⊂⎕DMX.(('EM' EM)('Message' Message)('EN' EN))
      ⍺← ⍬ ⋄ o← ⍺ ⋄ md← Flat ⍵ 
      s hj← o MergeOpts ⎕SRC ⎕THIS 
      html← hj InsertMD md                            ⍝ Insert the markdown text into the HTML/JS code   
      r← s HtmlRender html                            ⍝ Render and return the HTML object
      r⊣ r.MD← ⍵                                      ⍝ Make a private copy of the markdown from the user...
  }
  ⍝ *** Here ***
  ⍝ Here: CVV← token@CV ∇ CVV                    
  ⍝   Find payload in char vectors (CV) following ('^\h*⍝',token,'\h|$') in a vector of CV's. 
  ⍝     - If the token is 'XX', we match /^\h*⍝XX/ followed by /\h|$/. 
  ⍝       I.e., it will match XX, but not (simple) X, XY, XXX, etc.
  ⍝     - If the "token" is 'XX?' or 'X{1,2}', we will match X, XX, but not XY or XXX.
  ⍝   What follows the token and any following blank is the payload /(.*)/'. 
  Here← {  
    pfx src← ⍺ ⍵
    re←'^\h*⍝', pfx, '(?:\h|$)(.*)'                        
    re ⎕S '\1'⊣ src 
  }
  ⍝ *** defaults ***
  ⍝ defaults: d← ∇
  ⍝   Show the default options in JSON format, including 
  ⍝  'size' and 'posn'  used on the HTMLRenderer page.
  ∇ d← defaults  ;s; p; defs
    s←    '   size: [', ']',⍨ 1↓∊',',¨⍕¨sizeDef 
    p←    '   posn: [', ']',⍨ 1↓∊',',¨⍕¨posnDef 
    defs← '^\s+' ⎕R '   ' RE._Simple⊢ 'JO' Here ⎕SRC ⎕THIS 
    d← '{', CR, s, CR, p, CR, defs, '}'  
  ∇
  ⍝ example: e← ∇
  ⍝   A markdown example.  
  ∇ e← example                                         
    e← { 0=≢⍵: exampleT⊢← 'EX' Here ⎕SRC ⎕THIS ⋄ ⍵ } exampleT 
  ∇
  ∇ wait← help  ;r
    r← ('size' (900 900)) Show 'HLP' Here ⎕SRC ⎕THIS 
    wait←⍞⊢⍞←'> '
  ∇

⍝ -------------------------------------------------------------------------------------------
⍝ Constants
  ⎕IO ⎕ML← 0 1 
  CR← ⎕UCS 13
⍝ -------------------------------------------------------------------------------------------
⍝ Variables 
  sizeDef posnDef← (800 1000) (5 5)                  ⍝ size: height, width; posn: y, x 
  exampleT← ''                                       ⍝ See  ∇ example ∇  
:EndSection

:Section Internal_Utilities
⍝ -------------------------------------------------------------------------------------------
⍝ Internal Utilities
  ⍝ *** InsertMD ***
  ⍝ InsertMD:   CVV← CVV ∇ CVV                             
  ⍝   Insert ⍺:markdown into ⍵:html at ___MYTEXT___
  ⍝   Don't process escape chars in the replacement field...
  InsertMD← {  
      '^\h*___MYTEXT___.*$'  ⎕R ⍵ RE._Simple RE._Once RE._RE10⊢ ⍺ 
  }
  ⍝ *** Flat ***
  ⍝ Flat:  CcrV← ∇ CVV                               
  ⍝   Convert vector of char vectors into a CV with carriage returns.
  Flat← {¯1↓ ∊⍵,¨ CR}⊆
  
⍝ ⎕R options we use...
  :Namespace RE
     _Simple← ⍠('ResultText' 'Simple')('EOL' 'CR')
     _Once←   ⍠'ML' 1
     _RE10←   ⍠'Regex' (1 0)
  :EndNamespace 
  
  ⍝ *** HtmlRender ***
  ⍝ HtmlRender: ns.htmlObj@HTMLRenderer_obj← size@I2 posn@I2 ∇ html@CVV
  ⍝   Returns an html renderer object generated by ⎕WC.
  HtmlRender← {  
    s p← ⍺ 
    parms← ('HTML',⍥⊂ ⍵) (s,⍨ ⊂'Size') (p,⍨ ⊂'Posn') ('Coord' 'ScaledPixel')
    ns← #.⎕NS⍬                                       ⍝ Private ns for generated obj 
    _← 'ns.htmlObj' ⎕WC 'HTMLRenderer',⍥⊆ parms     ⍝ Generate the renderer as ns.htmlObj. 
    ns.htmlObj                                        ⍝ Return the generated object itself.
  }  

  ⍝ *** MergeOpts ***
  ⍝ MergeOpts: 
  ⍝    ∘ Load old Markdown options (in JSON format);
  ⍝    ∘ Merge any new options passed from APL, replacing 0 and 1 with (⊂'false') and (⊂'true'); 
  ⍝    ∘ Separate off the pseudo-option `size: [n1 n2]` and returning separately as (n1 n2);
  ⍝    ∘ Replace the ___OPTS___ stub in the HTML code with the up-to-date JSON options.
    ⍝ JMerge: 
    ⍝   ∘ Merge new APL-specified options into existing Json options
    ⍝   ∘ Returning the new or default size@IV[2]
    ⍝ ((sizeOut@I[2] posnOut@I[2]) jsonOut@CVV)← [jsonIn←''] (sizeDef@I[2] posnDef@I[2] ∇) opt1 [opt2 ...]
    ⍝ jsonIn:
    ⍝   a JSON5 list of key-value pairs or null.
    ⍝ sizeDef, posnDef
    ⍝   the default size/posn variables for use in an HTMLRenderer call.
    ⍝   ∘ Each has 2 items: (integers: height, width) and posn (integers: y and x offsets)
    ⍝   ∘ Their value  will be the ¨size¨ and ¨posn¨ returned, unless an option overrides it.
    ⍝ optN:
    ⍝   an APL-style key-value pair of the form ('Name' value).
    ⍝   A value of 1 or 0 will be replaced by ⊂'true' or ⊂'false', respectively.
    ⍝   Special case: keys 'size' and 'posn' will have their value replace the default size.
    ⍝       The size value must be of the form (height width);
    ⍝       The posn value must be of the form (y and x offsets):
    ⍝       ('size' (1000 600)) and ('posn' (5 5)) 
    ⍝ sizeOut, posnOut
    ⍝   The values set per above, either the defaults or explicitly set values.
    ⍝ jsonOut:
    ⍝   The 2nd element returned; a char. string representing the udpated
    ⍝   JSON5 key-value pairs.
  MergeOpts← { 
    JMerge←{
        T F← ⊂∘⊂¨'true' 'false'   ⍝ JSON true (1) and false (0)
        Json← ⎕JSON⍠'Dialect' 'JSON5'
        JImport← {0=≢⍵:⎕NS ⍬ ⋄ Json ⍵}
        Canon← { ,∘⊂⍣(2≥|≡⍵)⊢ ⍵ }
        J2A← { 
          ⍺ ⍺⍺.{
            2=≢⍵:⍎⍺,'←⍵'
            em← 'Options must consist of exactly two items: a key and a (scalar) value'
            em ⎕SIGNAL 11
          }⊃T F ⍵/⍨1,⍨1 0≡¨ ⊂⍵ 
        }
        GetHOpt← 'ns.' { 0≠ ⎕NC ⍺⍺,⍺: (⎕EX ⍺⍺,⍺)⊢ ⎕OR ⍺⍺,⍺ ⋄ ⍵ }
        ⍺← '{}' ⋄ j (s p)← ⍺ ⍺⍺
      0=≢⍵:  j,⍨⍥⊂ s p 
        ns← JImport j ⋄ _← (ns J2A)/¨ Canon ⍵
        (Json ns),⍨⍥⊂ ('size' GetHOpt s) ('posn' GetHOpt p)
    }
    optsApl src← ⍺ ⍵ 
    jStub← '___OPTS___'
    jOld← '{', CR, (Flat 'JO' Here src), CR, '}'                 ⍝ J: Default JSON
    sp jNow← jOld (sizeDef posnDef JMerge) optsApl              ⍝ sp: size pair and posn pair
    JUpdate← jStub ⎕R jNow RE._Simple RE._Once                      
    sp,⍥⊂ JUpdate 'HT' Here src                                  ⍝ H: Includes stub for JSON
  } 
:EndSection

:Section Example 
⍝ -------------------------------------------------------------------------------------------
⍝  example: Markdown example source 
⍝EX # An example of *Markdown* in the ***Showdown*** dialect
⍝EX
⍝EX ## A Paragraph
⍝EX This is a paragraph with **bold** text and this Emoji smile :smile: is generated via 
⍝EX the expression :smile\:.  By ***default***, we have set **simpleLineBreaks: false**, so 
⍝EX a single paragraph can be generated from multiple contiguous lines.
⍝EX We have four such lines here making one paragraph. This face 😜 is represented _directly_ in APL. 
⍝EX
⍝EX **Note**:
⍝EX If you want contiguous lines to include linebreaks, set ***('simpleLineBreaks' 1)***
⍝EX in the *APL* options.
⍝EX 
⍝EX 1. This is a bullet
⍝EX      * This is a *sub-*bullet.
⍝EX           * A sub***ber*** bullet.
⍝EX           * And another!
⍝EX 1. This is another top-level bullet. 
⍝EX 1. As is this.
⍝EX      We right now do NOT allow simplified autolinks to places like http://www.dyalog.com.
⍝EX
⍝EX     > A blockquote would look great here...
⍝EX
⍝EX 1. A final bullet?
⍝EX 
⍝EX ### Tonnage of [Columbus' Ships](http://columbuslandfall.com/ccnav/ships.shtml)\. 
⍝EX 
⍝EX   | Ship  | Niña    | Pinta | Santa Maria |
⍝EX   |: ---- |: ----- :|:-----:|:-----:|
⍝EX   | Type | caravel | caravel | carrack |
⍝EX   | Tonnage | 50-60 tons   | 70 tons  | 100 tons |
⍝EX   | Perceived size | ~~big~~| ~~bigger~~ | ~~gigantic~~ |
⍝EX   | Actual size| shrimpy shrimp | small shrimp | jumbo shrimp |
⍝EX
⍝EX **Note**: The above link to Columbus' Ships is an *explicit* link.
⍝EX
⍝EX ----
⍝EX 
⍝EX This is code: `⍳2` 
⍝EX 
⍝EX This is *also* code: <code>⍳3</code> 
⍝EX 
⍝EX And so is this:
⍝EX 
⍝EX      ⍝ Set off with 6 blanks
⍝EX        ∇ P← A IOTA B
⍝EX          P← A ⍳ B
⍝EX        ∇
⍝EX
⍝EX This should work. Does it? (**Yes**)
⍝EX ```
⍝EX +/⍺⍳⍵
⍝EX -\⍵⍳⍺
⍝EX ```
⍝EX
⍝EX ### What about tasks?
⍝EX + [x] This task is done
⍝EX - [ ] This is still pending
⍝EX + [x] We knocked this out of the park!
⍝EX 
⍝EX ### Goodbye:exclamation::exclamation::exclamation:
⍝EX 
:EndSection 

:Section HTML_Code 
⍝ -------------------------------------------------------------------------------------------
⍝  Markdown-to-Html code-- "showdown" javascript
⍝HT <!DOCTYPE html>
⍝HT <html>
⍝HT <head>
⍝HT   <title>Showdown Example</title>
⍝HT   <script src="https://cdnjs.cloudflare.com/ajax/libs/showdown/2.1.0/showdown.min.js" 
⍝HT        integrity="sha512-LhccdVNGe2QMEfI3x4DVV3ckMRe36TfydKss6mJpdHjNFiV07dFpS2xzeZedptKZrwxfICJpez09iNioiSZ3hA==" 
⍝HT        crossorigin="anonymous" referrerpolicy="no-referrer">
⍝HT   </script>
⍝HT </head>
⍝HT <body>
⍝HT   <div id="markdown-content" style="display:none;">
⍝HT     ___MYTEXT___          // User Markdown will replace this entire line!
⍝HT   </div>
⍝HT   <div id="html-content"></div>
⍝HT   <script>
⍝HT     var markdownText = document.getElementById('markdown-content').textContent;
⍝HT     var opts = ___OPTS___;    // Stub for JSON options
⍝HT     const converter = new showdown.Converter(opts);
⍝HT     const html = converter.makeHtml(markdownText);
⍝HT     document.getElementById('html-content').innerHTML = html;
⍝HT   </script>
⍝HT </body>
⍝HT </html>
:EndSection 

:Section Json Options
⍝ -------------------------------------------------------------------------------------------
⍝  Json Option Defaults. Used in place of ___OPTS___ above 
⍝     var opts = {
⍝        // For all options except ghCodeBlocks, the DEFAULT value is false
⍝        // Simple line break: If true, simple line break in paragraph emits <br>.
⍝        //                    If false (default), simple line break does not emit <br>.
⍝JO          simpleLineBreaks: false, 
⍝        // Enable tables 
⍝JO          tables: true,
⍝        // Enable strikethrough 
⍝JO          strikethrough: true,
⍝        // Omit extra line break in code blocks
⍝JO          omitExtraWLInCodeBlocks: true,
⍝        // Enable GitHub-compatible header IDs
⍝JO          ghCompatibleHeaderId: true,
⍝        // Fenced code blocks. True (default), enable code blocks with ``` ... ``` 
⍝JO          ghCodeBlocks: true,
⍝        // Prefix header IDs with "custom-id-"
⍝JO          prefixHeaderId: 'custom-id-',
⍝        // Enable emoji support 
⍝JO          emoji: true,
⍝        // Enable task lists 
⍝JO          tasklists: true,
⍝        // Disable automatic wrapping of HTML blocks
⍝JO          noHTMLBlocks: false,
⍝        // Allow simple URLs like http://dyalog.com in text to be treated as actual links. 
⍝        // Keep in mind that selecting a link will leave the Markdown page, w/o an easy way  
⍝        // to return (except by recreating the page).
⍝JO          simplifiedAutoLink: false,           
⍝    }
:EndSection 

:Section Help 
⍝HLP ## Markdown Utility (namespace)
⍝HLP 
⍝HLP - Use Markdown in an HTMLRenderer session in Dyalog
⍝HLP - Based on the *Showdown* dialect of *Markdown*
⍝HLP 
⍝HLP | Routine | Usage | &nbsp;&nbsp; | Type ||
⍝HLP |: ---- |: --- | --- :|: --- :|: --- |
⍝HLP | Show | Process and Display Markdown text | Ns← [opts] | ∇ | CVV |
⍝HLP |      | via the HTMLRenderer              |   |  |  |
⍝HLP | example | A bells-and-whistles Markdown example |CVV←| ∇ ||
⍝HLP | help | Display (this) help information || ∇ ||
⍝HLP | defaults | Show Markdown & HTMLRenderer defaults used |CV←|∇||
⍝HLP | Here | Pull Markdown from APL comments in current class  | CVV← pfx |∇ | ⎕SRC ⎕THIS |
⍝HLP |      | Pull Markdown from APL comments in current function      | CVV← pfx |∇ | ⎕NR ⊂⎕XSI |
⍝HLP | Flat | Convert APL char vector of vectors to a simple char vector (with CR's) | CV← | ∇ | CVV |
⍝HLP 
⍝HLP 
⍝HLP ### Usage:
⍝HLP 
⍝HLP [**html**←]  [**options**] Markdown.Show **markdown** 
⍝HLP 
⍝HLP where **markdown** is 
⍝HLP 
⍝HLP     a vector of character vectors containing standard "Showdown-style" Markdown
⍝HLP 
⍝HLP and **options** are
⍝HLP 
⍝HLP     APL Variant (⍠) style specifications of HTMLRenderer or Markdown JSON5 options:      
⍝HLP                                
⍝HLP ### Options sent to HTMLRenderer
⍝HLP | Show option | What HTMLRenderer sees | 
⍝HLP |: ---- |: ----- | 
⍝HLP |   ('size' (800 1000))              | ('Size' 800 1000) |         
⍝HLP |   ('posn' (5 5))                   | ('Posn' 5 5) |  
⍝HLP 
⍝HLP ### Options converted to Json and sent to Javascript Markdown Showdown translator 
⍝HLP | Show option | What Markdown sees | 
⍝HLP |: ---- |: ----- |          
⍝HLP |   ('simpleLineBreaks' 0)           | simpleLineBreaks: false,  |            
⍝HLP |   ('tables' 1)                     | tables: true,      |                   
⍝HLP |   ('strikethrough' 1)              |  strikethrough: true,   |               
⍝HLP |   ('omitExtraWLInCodeBlocks' 1)    |  omitExtraWLInCodeBlocks: true,  |      
⍝HLP |   ('ghCompatibleHeaderId' 1)       |  ghCompatibleHeaderId: true, |          
⍝HLP |   ('ghCodeBlocks' 1)               |  ghCodeBlocks: true,   |                
⍝HLP |   ('prefixHeaderId' 'custom-id-')  |  prefixHeaderId: 'custom-id-',   |      
⍝HLP |   ('emoji' 1)                      |  emoji: true,           |               
⍝HLP |   ('tasklists' 1)                  |  tasklists: true,       |               
⍝HLP |   ('noHTMLBlocks' 0)               |  noHTMLBlocks: false,    |              
⍝HLP |   ('simplifiedAutoLink' 0)         |  simplifiedAutoLink: false  | 
⍝HLP   
⍝HLP  Note [1]: See **Showdown** documention, especially for the Github options.  
⍝HLP  Note [2]: Call **Markdown.defaults** for the list of option variables (shown in Javascript format).
⍝HLP 
⍝HLP ### Markdown.Show
⍝HLP Show returns the resulting HTML as a vector of character vectors.
⍝HLP 
⍝HLP      To see the returned HTML, store the result of ¨Show¨ in a variable:
⍝HLP
⍝HLP         html← Markdown.Show example
⍝HLP 
⍝HLP      To remove the returned HTML permanently, delete or reset the variable:
⍝HLP
⍝HLP         ⎕EX 'html'    OR     html←''
⍝HLP 
⍝HLP      To temporarily stop displaying the returned HTML, set html variable "visible" to 0:
⍝HLP
⍝HLP         html.visible←0     ⍝ To redisplay, html.visible←1
⍝HLP 
⍝HLP      See HTMLRenderer for other APL-side variables.
⍝HLP  
⍝HLP ### Markdown Utilities and Examples
⍝HLP #### Markdown.defaults 
⍝HLP     returns all the HTML-directed and Markdown Showdown-dialect Json variables.
⍝HLP 
⍝HLP #### Markdown.Here
⍝HLP     makes it easy to take comments in APL functions and return them as Markdown or HTML code.
⍝HLP 
⍝HLP        vv← 'pfx' Markdown.Here ⊃⎕XSI     ⍝ Find '⍝pfx' lines in the current function.
⍝HLP 
⍝HLP #### Markdown.Flat 
⍝HLP     converts a vector of character vectors to a flat char vector with carriage returns. 
⍝HLP 
⍝HLP #### Markdown.example 
⍝HLP     contains a nice example. 
⍝HLP 
⍝HLP To see the example source, do:
⍝HLP 
⍝HLP      a←Markdown.example
⍝HLP      )ed a
⍝HLP 
⍝HLP To see the result, do: 
⍝HLP  
⍝HLP        x← Markdown.(Show example)
⍝HLP 
⍝HLP #### Markdown.help
⍝HLP     shows help information for Markdown.
⍝HLP 
⍝HLP        Markdown.help 
⍝HLP  
:EndSection 

:EndNamespace 
